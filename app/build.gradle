apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"

  defaultConfig {
    applicationId "com.example.android.twitterclient"
    minSdkVersion 18
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  productFlavors {
    mock {
      applicationIdSuffix ".mock"
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  packagingOptions {
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  testCompile 'junit:junit:4.12'

  compile 'com.android.support:appcompat-v7:23.3.0'
  compile 'com.android.support:recyclerview-v7:23.3.0'
  compile 'com.android.support:design:23.3.0'

  apt 'com.google.dagger:dagger-compiler:2.1'
  compile 'com.google.dagger:dagger:2.1'
  compile 'com.jakewharton:butterknife:7.0.1'

  compile 'io.reactivex:rxjava:1.1.2'
  compile 'io.reactivex:rxandroid:1.1.0'
  compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
  compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.1'

  compile 'com.google.code.gson:gson:2.6.2'
  compile 'com.squareup.retrofit2:retrofit:2.0.1'
  compile 'com.squareup.retrofit2:retrofit-mock:2.0.1'
  compile 'com.squareup.retrofit2:converter-gson:2.0.1'
  compile 'com.squareup.retrofit2:adapter-rxjava:2.0.1'

  compile 'com.yqritc:recyclerview-flexibledivider:1.2.9'
  compile 'com.facebook.stetho:stetho:1.3.1'
  compile 'net.danlew:android.joda:2.9.3'

  testCompile 'org.mockito:mockito-all:1.10.19'
  testCompile 'org.hamcrest:hamcrest-all:1.3'

  // Android Testing Support Library's runner and rules
  androidTestCompile 'com.android.support.test:runner:0.4.1'
  androidTestCompile 'com.android.support.test:rules:0.4.1'

  // Espresso UI Testing dependencies.
  androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
}

// Remove mockRelease as it's not needed.
android.variantFilter { variant ->
  if(variant.buildType.name.equals('release')
      && variant.getFlavors().get(0).name.equals('mock')) {
    variant.setIgnore(true);
  }
}

/*
Resolves dependency versions across test and production APKs, specifically, transitive
dependencies. This is required since Espresso internally has a dependency on support-annotations.
*/
//noinspection GroovyAssignabilityCheck
configurations.all {
  resolutionStrategy.force "com.android.support:support-annotations:23.2.1"
}

/*
All direct/transitive dependencies shared between your test and production APKs need to be
excluded from the test APK! This is necessary because both APKs will contain the same classes. Not
excluding these dependencies from your test configuration will result in an dex pre-verifier error
at runtime. More info in this tools bug: (https://code.google.com/p/android/issues/detail?id=192497)
*/
configurations.compile.dependencies.each { compileDependency ->
  println "Excluding compile dependency: ${compileDependency.getName()}"
  configurations.androidTestCompile.dependencies.each { androidTestCompileDependency ->
    configurations.androidTestCompile.exclude module: "${compileDependency.getName()}"
  }
}
